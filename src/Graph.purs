module Graph where

import Prelude hiding (add)

import Control.Monad.Rec.Class (Step(..), tailRec)
import Data.Array (fromFoldable) as A
import Data.Array (uncons)
import Data.Foldable (class Foldable)
import Data.List (List(..), concat, reverse, (:))
import Data.List (fromFoldable) as L
import Data.Maybe (Maybe(..))
import Data.Newtype (class Newtype, unwrap)
import Data.Set (Set, singleton, unions)
import Data.Set (empty, map) as S
import Data.Set.NonEmpty (NonEmptySet, fromSet)
import Data.Set.NonEmpty as NES
import Data.String (joinWith)
import Dict (Dict)
import Util (type (√ó), Endo, definitely, (\\), (√ó), (‚àà))

type Edge = Vertex √ó Vertex
type HyperEdge = Vertex √ó NonEmptySet Vertex -- mostly a convenience

-- | Immutable graphs, optimised for lookup and building from (key, value) pairs.
class (Eq g, Vertices g, Semigroup g) <= Graph g where
   -- | Whether g contains a given vertex.
   elem :: Vertex -> g -> Boolean
   -- | outN and iN satisfy
   -- |   inN G = outN (op G)
   outN :: g -> Vertex -> Set Vertex
   inN :: g -> Vertex -> Set Vertex

   -- | Number of vertices in g.
   size :: g -> Int

   sources :: g -> Set Vertex
   sinks :: g -> Set Vertex

   -- | op (op g) = g
   op :: Endo g

   empty :: g
   fromEdgeList :: List HyperEdge -> g

newtype Vertex = Vertex String

class Vertices a where
   vertices :: a -> Set Vertex

instance (Functor f, Foldable f) => Vertices (f Vertex) where
   vertices = (singleton <$> _) >>> unions

instance (Functor f, Foldable f) => Vertices (Dict (f Vertex)) where
   vertices = (vertices <$> _) >>> unions

selectŒ±s :: forall f. Apply f => Foldable f => f Boolean -> f Vertex -> Set Vertex
selectŒ±s vùîπ vŒ± = unions ((if _ then singleton else const S.empty) <$> vùîπ <*> vŒ±)

selectùîπs :: forall f. Functor f => f Vertex -> Set Vertex -> f Boolean
selectùîπs vŒ± Œ±s = (_ ‚àà Œ±s) <$> vŒ±

outEdges' :: forall g. Graph g => g -> Vertex -> List Edge
outEdges' g Œ± = L.fromFoldable $ S.map (Œ± √ó _) (outN g Œ±)

outEdges :: forall g. Graph g => g -> Set Vertex -> List Edge
outEdges g Œ±s = concat (outEdges' g <$> L.fromFoldable Œ±s)

inEdges' :: forall g. Graph g => g -> Vertex -> List Edge
inEdges' g Œ± = L.fromFoldable $ S.map (_ √ó Œ±) (inN g Œ±)

inEdges :: forall g. Graph g => g -> Set Vertex -> List Edge
inEdges g Œ±s = concat (inEdges' g <$> L.fromFoldable Œ±s)

toEdgeList :: forall g. Graph g => g -> List HyperEdge
toEdgeList g =
   tailRec go (A.fromFoldable (vertices g \\ sinks g) √ó Nil)
   where
   go :: Array Vertex √ó List HyperEdge -> Step _ (List HyperEdge)
   go (Œ±s' √ó acc) = case uncons Œ±s' of
      Nothing -> Done acc
      Just { head: Œ±, tail: Œ±s } ->
         Loop (Œ±s √ó (Œ± √ó definitely "non-empty" (fromSet (outN g Œ±))) : acc)

showGraph :: forall g. Graph g => g -> String
showGraph g =
   "digraph G {\n" <> joinWith "\n" lines <> "\n}"
   where
   lines :: Array String
   lines = indent <$> ([ "rankdir = RL" ] <> edges)

   edges :: Array String
   edges = showEdge <$> A.fromFoldable (reverse (toEdgeList g))

   indent :: Endo String
   indent = ("   " <> _)

   showEdge :: HyperEdge -> String
   showEdge (Œ± √ó Œ±s) =
      unwrap Œ± <> " -> {" <> joinWith ", " (A.fromFoldable $ unwrap `NES.map` Œ±s) <> "}"

derive instance Eq Vertex
derive instance Ord Vertex
derive instance Newtype Vertex _

instance Show Vertex where
   show (Vertex Œ±) = "Vertex " <> Œ±
