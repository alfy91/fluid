module App.Fig where

import Prelude hiding (absurd)

import App.BarChart (BarChart, barChartHandler, drawBarChart)
import App.CodeMirror (EditorView, dispatch, getContentsLength, update)
import App.LineChart (LineChart, drawLineChart, lineChartHandler)
import App.MatrixView (MatrixView(..), drawMatrix, matrixViewHandler, matrixRep)
import App.TableView (EnergyTable(..), drawTable, energyRecord, tableViewHandler)
import App.Util (HTMLId, OnSel, Selector, doNothing, from, record)
import Bindings (Var)
import Control.Monad.Error.Class (class MonadError)
import Data.Array (range, zip)
import Data.Either (Either(..))
import Data.Foldable (length)
import Data.List (List(..), (:), singleton)
import Data.Set (singleton) as S
import Data.Traversable (sequence, sequence_)
import Data.Tuple (fst, uncurry)
import DataType (cBarChart, cCons, cLineChart, cNil)
import Desugarable (desug)
import Dict (get)
import Effect (Effect)
import Effect.Aff.Class (class MonadAff)
import Effect.Console (log)
import Effect.Exception (Error)
import Eval (eval, eval_module)
import EvalBwd (evalBwd)
import EvalGraph (GraphConfig)
import Expr (Expr)
import Foreign.Object (lookup)
import Graph.GraphImpl (GraphImpl)
import Lattice (ùîπ, bot, botOf, erase, neg, topOf)
import Module (File(..), Folder(..), blah, defaultImports2, loadFile, open, openDatasetAs, openDefaultImports)
import Partial.Unsafe (unsafePartial)
import Pretty (prettyP)
import Primitive (matrixRep) as P
import SExpr (Expr(..), Module(..), RecDefs, VarDefs) as S
import SExpr (desugarModuleFwd)
import Trace (Trace)
import Util (type (√ó), type (+), (√ó), absurd, error, orElse)
import Val (class Ann, Env, ProgCxtEval(..), Val(..), append_inv, (<+>))
import Web.Event.EventTarget (eventListener)

data View
   = MatrixFig MatrixView
   | EnergyTableView EnergyTable
   | LineChartFig LineChart
   | BarChartFig BarChart

drawView :: HTMLId -> OnSel -> Int -> View -> Effect Unit
drawView divId onSel n (MatrixFig vw) = drawMatrix divId n vw =<< eventListener (onSel <<< matrixViewHandler)
drawView divId onSel n (EnergyTableView vw) = drawTable divId n vw =<< eventListener (onSel <<< tableViewHandler)
drawView divId onSel n (LineChartFig vw) = drawLineChart divId n vw =<< eventListener (onSel <<< lineChartHandler)
drawView divId onSel n (BarChartFig vw) = drawBarChart divId n vw =<< eventListener (onSel <<< barChartHandler)

-- Convert sliced value to appropriate View, discarding top-level annotations for now.
-- 'from' is partial; encapsulate that here.
view :: String -> Val ùîπ -> View
view _ (Constr _ c (u1 : Nil)) | c == cBarChart =
   BarChartFig (unsafePartial $ record from u1)
view _ (Constr _ c (u1 : Nil)) | c == cLineChart =
   LineChartFig (unsafePartial $ record from u1)
view title u@(Constr _ c _) | c == cNil || c == cCons =
   EnergyTableView (EnergyTable { title, table: unsafePartial $ record energyRecord <$> from u })
view title u@(Matrix _ _) =
   MatrixFig (MatrixView { title, matrix: matrixRep $ fst (P.matrixRep.match u) })
view _ _ = error absurd

-- An example of the form (let <defs> in expr) can be decomposed as follows.
type SplitDefs a =
   { Œ≥ :: Env a -- local env (additional let bindings at beginning of ex)
   , s :: S.Expr a -- body of example
   }

-- Decompose as above.
splitDefs :: forall a m. Ann a => MonadError Error m => Env a -> S.Expr a -> m (SplitDefs a)
splitDefs Œ≥0 s' = do
   let defs √ó s = unsafePartial $ unpack s'
   Œ≥ <- desugarModuleFwd (S.Module (singleton defs)) >>= flip (eval_module Œ≥0) bot
   pure { Œ≥, s }
   where
   unpack :: Partial => S.Expr a -> (S.VarDefs a + S.RecDefs a) √ó S.Expr a
   unpack (S.LetRec defs s) = Right defs √ó s
   unpack (S.Let defs s) = Left defs √ó s

type FigSpec =
   { divId :: HTMLId
   , file :: File
   , xs :: Array Var -- variables to be considered "inputs"
   }

type Fig =
   { spec :: FigSpec
   , Œ≥0 :: Env ùîπ -- ambient env (default imports)
   , Œ≥ :: Env ùîπ -- local env (loaded dataset, if any, plus additional let bindings at beginning of ex)
   , s0 :: S.Expr ùîπ -- program that was originally "split"
   , s :: S.Expr ùîπ -- body of example
   , e :: Expr ùîπ -- desugared s
   , t :: Trace
   , v :: Val ùîπ
   }

type LinkFigSpec =
   { divId :: HTMLId
   , file1 :: File
   , file2 :: File
   , dataFile :: File
   , x :: Var
   }

type LinkFig =
   { spec :: LinkFigSpec
   , Œ≥ :: Env ùîπ -- prog context environment (modules + dataset)
   , s1 :: S.Expr ùîπ
   , s2 :: S.Expr ùîπ
   , e1 :: Expr ùîπ
   , e2 :: Expr ùîπ
   , t1 :: Trace
   , t2 :: Trace
   , v1 :: Val ùîπ
   , v2 :: Val ùîπ
   , v0 :: Val ùîπ -- common data named by spec.x
   , dataFile :: String -- TODO: provide surface expression instead and prettyprint
   }

type LinkResult =
   { v' :: Val ùîπ -- will represent either v1' or v2'
   , v0' :: Val ùîπ
   }

drawLinkFig :: LinkFig -> EditorView -> EditorView -> EditorView -> Selector Val + Selector Val -> Effect Unit
drawLinkFig fig@{ spec: { x, divId }, Œ≥, s1, s2, e1, e2, t1, t2, v1, v2, dataFile } ed1 ed2 ed3 Œ¥v = do
   log $ "Redrawing " <> divId
   v1' √ó v2' √ó Œ¥v1 √ó Œ¥v2 √ó v0 <- case Œ¥v of
      Left Œ¥v1 -> do
         let v1' = Œ¥v1 v1
         { v', v0' } <- linkResult x Œ≥ e1 e2 t1 t2 v1'
         pure $ v1' √ó v' √ó const v1' √ó identity √ó v0'
      Right Œ¥v2 -> do
         let v2' = Œ¥v2 v2
         { v', v0' } <- linkResult x Œ≥ e2 e1 t2 t1 v2'
         pure $ v' √ó v2' √ó identity √ó const v2' √ó v0'
   drawView divId (\selector -> drawLinkFig fig ed1 ed2 ed3 (Left $ Œ¥v1 >>> selector)) 2 $ view "left view" v1'
   drawView divId (\selector -> drawLinkFig fig ed1 ed2 ed3 (Right $ Œ¥v2 >>> selector)) 0 $ view "right view" v2'
   drawView divId doNothing 1 $ view "common data" v0
   drawCode ed1 $ prettyP s1
   drawCode ed2 $ prettyP s2
   drawCode ed3 $ dataFile

drawCode :: EditorView -> String -> Effect Unit
drawCode ed s = do
   tr <- update ed.state [ { changes: { from: 0, to: getContentsLength ed, insert: s } } ]
   dispatch ed tr

drawFig :: Fig -> EditorView -> Selector Val -> Effect Unit
drawFig fig@{ spec: { divId }, s0 } ed Œ¥v = do
   log $ "Redrawing " <> divId
   v_view √ó views <- figViews fig Œ¥v
   sequence_ $
      uncurry (drawView divId doNothing) <$> zip (range 0 (length views - 1)) views
   drawView divId (\selector -> drawFig fig ed (Œ¥v >>> selector)) (length views) v_view
   drawCode ed $ prettyP s0

varView :: forall m. MonadError Error m => Var -> Env ùîπ -> m View
varView x Œ≥ = view x <$> (lookup x Œ≥ # orElse absurd)

valViews :: forall m. MonadError Error m => Env ùîπ -> Array Var -> m (Array View)
valViews Œ≥ xs = sequence (flip varView Œ≥ <$> xs)

-- For an output selection, views of corresponding input selections and output after round-trip.
figViews :: forall m. MonadError Error m => Fig -> Selector Val -> m (View √ó Array View)
figViews { spec: { xs }, Œ≥0, Œ≥, e, t, v } Œ¥v = do
   let
      Œ≥0Œ≥ √ó e' √ó Œ± = evalBwd (erase <$> (Œ≥0 <+> Œ≥)) (erase e) (Œ¥v v) t
   _ √ó v' <- eval Œ≥0Œ≥ e' Œ±
   views <- valViews Œ≥0Œ≥ xs
   pure $ view "output" v' √ó views

linkResult :: forall m. MonadError Error m => Var -> Env ùîπ -> Expr ùîπ -> Expr ùîπ -> Trace -> Trace -> Val ùîπ -> m LinkResult
linkResult x Œ≥0Œ≥ e1 e2 t1 _ v1 = do
   let
      Œ≥0Œ≥' √ó _ = evalBwd (erase <$> Œ≥0Œ≥) (erase e1) v1 t1
      Œ≥0' √ó Œ≥' = append_inv (S.singleton x) Œ≥0Œ≥'
   v0' <- lookup x Œ≥' # orElse absurd
   -- make Œ≥0 and e2 fully available; Œ≥0 was previously too big to operate on, so we use
   -- (topOf Œ≥0) combined with negation of the dataset environment slice
   _ √ó v2' <- eval (neg ((botOf <$> Œ≥0') <+> Œ≥')) (topOf e2) true
   pure { v': neg v2', v0' }

loadFig :: forall m. MonadAff m => MonadError Error m => FigSpec -> m Fig
loadFig spec@{ file } = do
   { progCxt: ProgCxtEval { Œ≥ } } :: GraphConfig GraphImpl <- defaultImports2 >>= blah
   let Œ≥0 = botOf <$> Œ≥
   s' <- open file
   let s0 = botOf s'
   { Œ≥: Œ≥1, s } <- splitDefs Œ≥0 s0
   e <- desug s
   let Œ≥0Œ≥ = Œ≥0 <+> Œ≥1
   t √ó v <- eval Œ≥0Œ≥ e bot
   pure { spec, Œ≥0, Œ≥: Œ≥0 <+> Œ≥1, s0, s, e, t, v }

loadLinkFig :: forall m. MonadAff m => MonadError Error m => LinkFigSpec -> m LinkFig
loadLinkFig spec@{ file1, file2, dataFile, x } = do
   let
      dir = File "linking/"
      name1 √ó name2 = (dir <> file1) √ó (dir <> file2)
   -- views share ambient environment Œ≥ as well as dataset
   { progCxt: ProgCxtEval { Œ≥ } } √ó xv :: GraphConfig GraphImpl √ó _ <-
      openDefaultImports >>= openDatasetAs (File "example/" <> dir <> dataFile) x
   s1' √ó s2' <- (√ó) <$> open name1 <*> open name2
   let
      Œ≥0 = botOf <$> (Œ≥ <+> xv)
      s1 = botOf s1'
      s2 = botOf s2'
   dataFile' <- loadFile (Folder "fluid/example/linking") dataFile -- use surface expression instead
   e1 √ó e2 <- (√ó) <$> desug s1 <*> desug s2
   t1 √ó v1 <- eval Œ≥0 e1 bot
   t2 √ó v2 <- eval Œ≥0 e2 bot
   let v0 = get x Œ≥0
   pure { spec, Œ≥: Œ≥0, s1, s2, e1, e2, t1, t2, v1, v2, v0, dataFile: dataFile' }
