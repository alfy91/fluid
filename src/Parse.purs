module Parse where

import Prelude hiding (absurd, add, between, join)
import Control.Alt ((<|>))
import Control.Apply (lift2)
import Control.Lazy (fix)
import Control.MonadPlus (empty)
import Data.Array (elem, fromFoldable)
import Data.Either (choose)
import Data.Function (on)
import Data.Identity (Identity)
import Data.List (List(..), (:), concat, foldr, groupBy, singleton, snoc, sortBy)
import Data.List.NonEmpty (NonEmptyList(..), toList)
import Data.Map (values)
import Data.NonEmpty ((:|))
import Data.Ordering (invert)
import Data.Profunctor.Choice ((|||))
import Text.Parsing.Parser.Combinators (between, try)
import Text.Parsing.Parser.Expr (Operator(..), OperatorTable, buildExprParser)
import Text.Parsing.Parser.Language (emptyDef)
import Text.Parsing.Parser.String (char, eof, oneOf)
import Text.Parsing.Parser.Token (
  GenLanguageDef(..), LanguageDef, TokenParser, alphaNum, letter, makeTokenParser, unGenLanguageDef
)
import Bindings (Var)
import DataType (Ctr(..), cPair, isCtrName, isCtrOp)
import Lattice (ùîπ)
import Primitive (opDefs)
import SExpr (
   Branch, Clause, Expr(..), ListRest(..), ListRestPattern(..), Module(..), Pattern(..), Qualifier(..),
   RecDefs, VarDef(..), VarDefs
)
import Util (Endo, type (√ó), (√ó), type (+), error, onlyIf)
import Util.Parse (SParser, sepBy_try, sepBy1, sepBy1_try, some)

-- constants (should also be used by prettyprinter)
strArrayLBracket  = "[|"       :: String
strArrayRBracket  = "|]"       :: String
strAs             = "as"      :: String
strBackslash      = "\\"      :: String
strBar            = "|"       :: String
strEllipsis       = ".."      :: String
strElse           = "else"    :: String
strEquals         = "="       :: String
strFun            = "fun"     :: String
strIf             = "if"      :: String
strIn             = "in"      :: String
strLBracket       = "["       :: String
strLet            = "let"     :: String
strMatch          = "match"   :: String
strLArrow         = "<-"      :: String
strRArrow         = "->"      :: String
strRBracket       = "]"       :: String
strThen           = "then"    :: String

languageDef :: LanguageDef
languageDef = LanguageDef (unGenLanguageDef emptyDef) {
   commentStart = "{-",
   commentEnd = "-}",
   commentLine = "--",
   nestedComments = true,
   identStart = letter <|> char '_',
   identLetter = alphaNum <|> oneOf ['_', '\''],
   opStart = opChar,
   opLetter = opChar,
   reservedOpNames = [strBar, strEllipsis, strEquals, strLArrow, strRArrow],
   reservedNames = [strAs, strElse, strFun, strIf, strIn, strLet, strMatch, strThen],
   caseSensitive = true
} where
   opChar :: SParser Char
   opChar = oneOf [
      ':', '!', '#', '$', '%', '&', '*', '+', '.', '/', '<', '=', '>', '?', '@', '\\', '^', '|', '-', '~'
   ]

token :: TokenParser
token = makeTokenParser languageDef

lArrow :: SParser Unit
lArrow = token.reservedOp strLArrow

lBracket :: SParser Unit
lBracket = void $ token.symbol strLBracket

bar :: SParser Unit
bar = token.reservedOp strBar

ellipsis :: SParser Unit
ellipsis = token.reservedOp strEllipsis

equals :: SParser Unit
equals = token.reservedOp strEquals

rBracket :: SParser Unit
rBracket = void $ token.symbol strRBracket

rArrow :: SParser Unit
rArrow = token.reservedOp strRArrow

-- 'reserved' parser only checks that str isn't a prefix of a valid identifier, not that it's in reservedNames.
keyword ‚à∑ String ‚Üí SParser Unit
keyword str =
   if str `elem` (unGenLanguageDef languageDef).reservedNames
   then token.reserved str
   else error $ str <> " is not a reserved word"

ident ‚à∑ SParser Var
ident = do
   x <- token.identifier
   onlyIf (not $ isCtrName x) x

ctr :: SParser Ctr
ctr = do
   x <- token.identifier
   onlyIf (isCtrName x) $ Ctr x

simplePattern :: Endo (SParser Pattern)
simplePattern pattern' =
   try listEmpty <|>
   listNonEmpty <|>
   try constr <|>
   try var <|>
   try (token.parens pattern') <|>
   pair

   where
   listEmpty :: SParser Pattern
   listEmpty = token.brackets $ pure $ PListEmpty

   listNonEmpty :: SParser Pattern
   listNonEmpty = lBracket *> (PListNonEmpty <$> pattern' <*> fix listRest)
      where
      listRest :: Endo (SParser ListRestPattern)
      listRest listRest' =
         rBracket *> pure PEnd <|>
         token.comma *> (PNext <$> pattern' <*> listRest')

   -- Constructor name as a nullary constructor pattern.
   constr :: SParser Pattern
   constr = PConstr <$> ctr <@> Nil

   -- TODO: anonymous variables
   var :: SParser Pattern
   var = PVar <$> ident

   pair :: SParser Pattern
   pair =
      token.parens $ do
         œÄ <- pattern' <* token.comma
         œÄ' <- pattern'
         pure $ PConstr cPair (œÄ : œÄ' : Nil)

patternDelim :: SParser Unit
patternDelim = rArrow <|> equals

-- "curried" controls whether nested functions are permitted in this context
branch :: Boolean -> SParser (Expr ùîπ) -> SParser Unit -> SParser (Branch ùîπ)
branch curried expr' delim = do
   œÄs <- if curried
         then some $ simplePattern pattern
         else NonEmptyList <$> pattern `lift2 (:|)` pure Nil
   e <- delim *> expr'
   pure $ œÄs √ó e

branch_curried :: SParser (Expr ùîπ) -> SParser Unit -> SParser (Branch ùîπ)
branch_curried expr' delim =
   some (simplePattern pattern) `lift2 (√ó)` (delim *> expr')

branch_uncurried :: SParser (Expr ùîπ) -> SParser Unit -> SParser (Pattern √ó Expr ùîπ)
branch_uncurried expr' delim =
   pattern `lift2 (√ó)` (delim *> expr')

branchMany :: forall b . SParser (Expr ùîπ) ->
              (SParser (Expr ùîπ) -> SParser Unit -> SParser b) ->
              SParser (NonEmptyList b)
branchMany expr' branch_ = token.braces $ sepBy1 (branch_ expr' rArrow) token.semi

branches :: forall b . SParser (Expr ùîπ) -> (SParser (Expr ùîπ) -> SParser Unit -> SParser b) -> SParser (NonEmptyList b)
branches expr' branch_ =
   (pure <$> branch_ expr' patternDelim) <|> branchMany expr' branch_

varDefs :: SParser (Expr ùîπ) -> SParser (VarDefs ùîπ)
varDefs expr' = keyword strLet *> sepBy1_try clause token.semi
   where
   clause :: SParser (VarDef ùîπ)
   clause = VarDef <$> (pattern <* equals) <*> expr'

recDefs :: SParser (Expr ùîπ) -> SParser (RecDefs ùîπ)
recDefs expr' = do
   keyword strLet *> sepBy1_try clause token.semi
   where
   clause :: SParser (Clause ùîπ)
   clause = ident `lift2 (√ó)` (branch_curried expr' equals)

defs :: SParser (Expr ùîπ) -> SParser (List (VarDefs ùîπ + RecDefs ùîπ))
defs expr' = singleton <$> choose (try $ varDefs expr') (recDefs expr')

-- Tree whose branches are binary primitives and whose leaves are application chains.
expr_ :: SParser (Expr ùîπ)
expr_ = fix $ appChain >>> buildExprParser (operators binaryOp)
   where
   -- Syntactically distinguishing infix constructors from other operators (a la Haskell) allows us to
   -- optimise an application tree into a (potentially partial) constructor application.
   binaryOp :: String -> SParser (Expr ùîπ -> Expr ùîπ -> Expr ùîπ)
   binaryOp op = do
      op' <- token.operator
      onlyIf (op == op') $
         if isCtrOp op'
         then \e e' -> Constr false (Ctr op') (e : e' : empty)
         else \e e' -> BinaryApp e op e'

   -- Left-associative tree of applications of one or more simple terms.
   appChain :: Endo (SParser (Expr ùîπ))
   appChain expr' = simpleExpr >>= rest
      where
      rest :: Expr ùîπ -> SParser (Expr ùîπ)
      rest e@(Constr Œ± c es) = ctrArgs <|> pure e
         where
         ctrArgs :: SParser (Expr ùîπ)
         ctrArgs = simpleExpr >>= \e' -> rest (Constr Œ± c (es <> (e' : empty)))
      rest e = ((App e <$> simpleExpr) >>= rest) <|> pure e

      -- Any expression other than an operator tree or an application chain.
      simpleExpr :: SParser (Expr ùîπ)
      simpleExpr =
         matrix <|> -- before list
         try nil <|>
         listNonEmpty <|>
         listComp <|>
         listEnum <|>
         try constr <|>
         try variable <|>
         try float <|>
         try int <|> -- int may start with +/-
         string <|>
         defsExpr <|>
         matchAs <|>
         try (token.parens expr') <|>
         try parensOp <|>
         pair <|>
         lambda <|>
         ifElse

         where
         matrix :: SParser (Expr ùîπ)
         matrix =
            between (token.symbol strArrayLBracket) (token.symbol strArrayRBracket) $
               Matrix false <$>
                  (expr' <* bar) <*>
                  token.parens (ident `lift2 (√ó)` (token.comma *> ident)) <*>
                  (keyword strIn *> expr')

         nil :: SParser (Expr ùîπ)
         nil = token.brackets $ pure (ListEmpty false)

         listNonEmpty :: SParser (Expr ùîπ)
         listNonEmpty =
            lBracket *> (ListNonEmpty false <$> expr' <*> fix listRest)

            where
            listRest :: Endo (SParser (ListRest ùîπ))
            listRest listRest' =
               rBracket *> pure (End false) <|>
               token.comma *> (Next false <$> expr' <*> listRest')

         listComp :: SParser (Expr ùîπ)
         listComp = token.brackets $
            pure (ListComp false) <*> expr' <* bar <*> sepBy1 qualifier (token.comma)

            where
            qualifier :: SParser (Qualifier ùîπ)
            qualifier =
               Generator <$> pattern <* lArrow <*> expr' <|>
               Declaration <$> (VarDef <$> (keyword strLet *> pattern <* equals) <*> expr') <|>
               Guard <$> expr'

         listEnum :: SParser (Expr ùîπ)
         listEnum = token.brackets $
            pure ListEnum <*> expr' <* ellipsis <*> expr'

         constr :: SParser (Expr ùîπ)
         constr = Constr false <$> ctr <@> empty

         variable :: SParser (Expr ùîπ)
         variable = ident <#> Var

         signOpt :: ‚àÄ a . Ring a => SParser (a -> a)
         signOpt = (char '-' $> negate) <|> (char '+' $> identity) <|> pure identity

         -- built-in integer/float parsers don't seem to allow leading signs.
         int :: SParser (Expr ùîπ)
         int = do
            sign <- signOpt
            (sign >>> Int false) <$> token.natural

         float :: SParser (Expr ùîπ)
         float = do
            sign <- signOpt
            (sign >>> Float false) <$> token.float

         string :: SParser (Expr ùîπ)
         string = Str false <$> token.stringLiteral

         defsExpr :: SParser (Expr ùîπ)
         defsExpr = do
            defs' <- concat <<< toList <$> sepBy1 (defs expr') token.semi
            foldr (\def -> (Let ||| LetRec) def) <$> (keyword strIn *> expr') <@> defs'

         matchAs :: SParser (Expr ùîπ)
         matchAs =
            MatchAs <$> (keyword strMatch *> expr' <* keyword strAs) <*> branches expr' branch_uncurried

         -- any binary operator, in parentheses
         parensOp :: SParser (Expr ùîπ)
         parensOp = Op <$> token.parens token.operator

         pair :: SParser (Expr ùîπ)
         pair = token.parens $
            (pure $ \e e' -> Constr false cPair (e : e' : empty)) <*> (expr' <* token.comma) <*> expr'

         lambda :: SParser (Expr ùîπ)
         lambda = Lambda <$> (keyword strFun *> branches expr' branch_curried)

         ifElse :: SParser (Expr ùîπ)
         ifElse = pure IfElse <*> (keyword strIf *> expr') <* keyword strThen <*> expr' <* keyword strElse <*> expr'

-- each element of the top-level list corresponds to a precedence level
operators :: forall a . (String -> SParser (a -> a -> a)) -> OperatorTable Identity String a
operators binaryOp =
   fromFoldable $ fromFoldable <$>
   (map (\({ op, assoc }) -> Infix (try $ binaryOp op) assoc)) <$>
   groupBy (eq `on` _.prec) (sortBy (\x -> comparing _.prec x >>> invert) $ values opDefs)

-- Pattern with no continuation.
pattern :: SParser Pattern
pattern = fix $ appChain_pattern >>> buildExprParser (operators infixCtr)
   where
   -- Analogous in some way to app_chain, but nothing higher-order here: no explicit application nodes,
   -- non-saturated constructor applications, or patterns other than constructors in the function position.
   appChain_pattern :: Endo (SParser Pattern)
   appChain_pattern pattern' = simplePattern pattern' >>= rest
      where
         rest ‚à∑ Pattern -> SParser Pattern
         rest œÄ@(PConstr c œÄs) = ctrArgs <|> pure œÄ
            where
            ctrArgs :: SParser Pattern
            ctrArgs = simplePattern pattern' >>= \œÄ' -> rest $ PConstr c (œÄs `snoc` œÄ')
         rest œÄ = pure œÄ

   infixCtr :: String -> SParser (Pattern -> Pattern -> Pattern)
   infixCtr op = do
      op' <- token.operator
      onlyIf (isCtrOp op' && op == op') \œÄ œÄ' -> PConstr (Ctr op') (œÄ : œÄ' : Nil)

topLevel :: forall a . Endo (SParser a)
topLevel p = token.whiteSpace *> p <* eof

program ‚à∑ SParser (Expr ùîπ)
program = topLevel expr_

module_ :: SParser (Module ùîπ)
module_ = Module <<< concat <$> topLevel (sepBy_try (defs expr_) token.semi <* token.semi)
